buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
    }
}

apply plugin: "java"

version = getVersion("EcoCore", "VERSION") + "-1.8.9"
def llibrary_version = getVersion("EcoCore", "LLIBRARY_VERSION")
group = "net.ecocraft.ecocore"
archivesBaseName = "ecocore"

allprojects {
    apply plugin: "net.minecraftforge.gradle.forge"

    sourceCompatibility = targetCompatibility = "1.7"

    minecraft {
        version = "1.8.9-11.15.1.1722"
        runDir = "minecraft"
        mappings = "stable_20"
    }

    repositories {
        maven {
            name = "ilexiconn"
            url = "http://maven.ilexiconn.net"
        }
    }

    dependencies {
        compile "net.ilexiconn:llibrary:$llibrary_version-1.8.9:dev"
    }

    processResources {
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version
        inputs.property "llibrary_version", llibrary_version

        from(sourceSets.main.resources.srcDirs) {
            include "mcmod.info"
            expand "version": project.version, "mcversion": project.minecraft.version, "llibrary_version": llibrary_version
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude "mcmod.info"
        }
    }
}

subprojects {
    dependencies {
        compile rootProject
    }
}

dependencies {
    runtime project(":ecoearth")
    runtime project(":ecofauna")
    runtime project(":ecoflora")
    runtime project(":ecoterra")
}

def getVersion(String main, String type) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String dir = main.toLowerCase()
    String prefix = "public static final String $type = \"";
    File mainfile = file("src/main/java/net/ecocraft/$dir/$main" + ".java")
    if (!main.equals("EcoCore")) {
        mainfile = file("$dir/src/main/java/net/ecocraft/$dir/$main" + ".java")
    }
    mainfile.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}