buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
    }
}

apply plugin: "java"

subprojects {
    subProject -> afterEvaluate{
        dependencies {
            compile project(":ecocore")
            compile "net.ilexiconn:llibrary:" + subProject.llibrary_version + "-1.8.9:dev"
        }
    
        processResources {
            inputs.property "version", subProject.version
            inputs.property "mcversion", subProject.minecraft.version
            inputs.property "llibrary_version", subProject.llibrary_version
    
            from(subProject.sourceSets.main.resources.srcDirs) {
                   include "mcmod.info"
                expand "version": project.version, "mcversion": project.minecraft.version, "llibrary_version": llibrary_version
            }
    
            from(subProject.sourceSets.main.resources.srcDirs) {
                exclude "mcmod.info"
            }
        }
    }
    apply plugin: "net.minecraftforge.gradle.forge"
    apply plugin: "java"

    sourceCompatibility = targetCompatibility = "1.7"

    minecraft {
        version = "1.8.9-11.15.1.1722"
        runDir = "minecraft"
        mappings = "stable_20"
    }

    repositories {
        maven {
            name = "ilexiconn"
            url = "http://maven.ilexiconn.net"
        }
    }
}

dependencies {
    compile project(":ecocore")
    compile project(":ecoearth")
    compile project(":ecofauna")
    compile project(":ecoflora")
    compile project(":ecoterra")
}

def getVersion(String main, String type) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String dir = main.toLowerCase()
    String prefix = "public static final String $type = \"";
    File mainfile = file("$dir/src/main/java/net/ecocraft/$dir/$main" + ".java")
    mainfile.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
